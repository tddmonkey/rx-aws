apply plugin: 'maven'
apply plugin: 'signing'

version = '0.0.1'
group = "com.github.tddmonkey"

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'Groovy Duration Extensions'
				packaging 'jar'
				description 'Groovy extensions for converting Numbers to a Duration'
				url 'https://github.com/tddmonkey/groovy-extensions'

				scm {
					connection 'scm:git:https://github.com/tddmonkey/groovy-extensions.git'
					developerConnection 'scm:git:https://github.com/tddmonkey/groovy-extensions.git'
					url 'https://github.com/tddmonkey/groovy-extensions.git'
				}

				licenses {
					license {
						name 'MIT'
						url 'https://opensource.org/licenses/MIT'
					}
				}

				developers {
					developer {
						id 'tddmonkey'
						name 'Colin Vipurs'
						email 'zodiaczx6@gmail.com'
					}
				}
			}
		}
	}
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

signing {
	sign configurations.archives
}


